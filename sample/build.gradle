/*
 * The MIT License (MIT)
 * Copyright © 2018 NBCO Yandex.Money LLC
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
 * associated documentation files (the “Software”), to deal in the Software without restriction, including
 * without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies
 * of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
 * following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or substantial
 * portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
 * PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
 * LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT
 * OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
 * OTHER DEALINGS IN THE SOFTWARE.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.triplet.play'

apply from: rootProject.file('common.gradle')

android {
    compileSdkVersion targetSdkVersion
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "ru.yandex.money.android.example"
        minSdkVersion 15
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        def buildDateMillis = Calendar.getInstance().toInstant().toEpochMilli()
        buildConfigField "long", "BUILD_DATE", "${buildDateMillis}L"

        buildConfigField "String", "MERCHANT_TOKEN", "${optionalProperty('merchantToken')}"
        buildConfigField "String", "GATEWAY_ID", "${optionalProperty('gatewayId')}"
        buildConfigField "String", "SHOP_ID", "${optionalProperty('shopId')}"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', '../library/proguard-rules.pro'
        }
    }

    defaultConfig {
        manifestPlaceholders = [YANDEX_CLIENT_ID: "${findProperty('yandexClientId')}"]
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${generateFileName(project, variant)}.apk"
        }
    }
}

play {
    jsonFile = getGooglePlayKeyFile()
    track = 'beta'
}

private File getGooglePlayKeyFile() {
    def templateFile = file('google-play-key.json')
    def keyFile = new File("play")
    return keyFile
}

repositories {
    maven { url 'https://dl.bintray.com/yandex-money/maven' }
    mavenCentral()
}

dependencies {
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.facebook.device.yearclass:yearclass:2.1.0'

    final leak_canary = '1.6.2'
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$leak_canary"

    releaseImplementation project(':library')
    debugImplementation project(':library')
    implementation 'com.yandex.android:authsdk:2.1.1'
    implementation 'cards.pay:paycardsrecognizer:1.1.0'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.robolectric:robolectric:3.8"
    androidTestImplementation "com.android.support.test:runner:$support_test_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
