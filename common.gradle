/*
 * The MIT License (MIT)
 * Copyright © 2018 NBCO Yandex.Money LLC
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
 * associated documentation files (the “Software”), to deal in the Software without restriction, including
 * without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies
 * of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
 * following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or substantial
 * portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
 * PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
 * LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT
 * OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
 * OTHER DEALINGS IN THE SOFTWARE.
 */

apply from: rootProject.file("sensitive.gradle")

ext {
    versionMajor = 1
    versionMinor = 3
    versionPatch = 0
    versionAlpha = 3

    versionCode = [versionMajor, versionMinor, versionPatch, versionAlpha].inject { result, i -> result * 100 + i }
    versionName = "$versionMajor.$versionMinor.$versionPatch" + (versionAlpha ? ".$versionAlpha" : "")

    generateFileName = { project, variant ->
        def fileNameParts = [
                variant.name,
                project.versionName,
                project.ext.has('teamcity.buildConfName') ? project.ext['teamcity.buildConfName'] : "local",
                project.ext.has('build.number') ? project.ext['build.number'] : "0"
        ]

        if (variant.productFlavors.find { it.name.equalsIgnoreCase("prod") }) {
            fileNameParts += optionalProperty('defaultProdYandexAuthToken') ? "hasYandexToken" : "noYandexToken"
            fileNameParts += optionalProperty('defaultProdPaymentAuthToken') ? "hasPaymentToken" : "noPaymentToken"
        } else if (variant.productFlavors.find { it.name.equalsIgnoreCase("dev") }) {
            fileNameParts += optionalProperty('defaultDevYandexAuthToken') ? "hasYandexToken" : "noYandexToken"
            fileNameParts += optionalProperty('defaultDevPaymentAuthToken') ? "hasPaymentToken" : "noPaymentToken"
        }

        return fileNameParts.join('-')
    }
}
